apply plugin: 'signing'

String signingKey = findProperty("signatory.key") ?: System.getenv("SIGNING_KEY")
String signingPassword = findProperty("signatory.password") ?: System.getenv("SIGNING_PASSWORD")

signing {
    if (signingKey != null &&
            signingPassword != null &&
            !signingKey.isEmpty() &&
            !signingPassword.isEmpty()) {
        useInMemoryPgpKeys(signingKey, signingPassword)
        sign publishing.publications.mavenJava
    } else {
        logger.warn('The signing key and password are null. This can be ignored if this is a pull request.')
    }
}

def isReleaseVersion = !version.endsWith("SNAPSHOT") && /* for Eclipse plugin */ !version.contains("-SNAPSHOT.")
tasks.withType(Sign) {
    onlyIf { isReleaseVersion }
}